# Copyright 2021 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

ts_library("build_ts") {
  root_dir = "."
  out_dir = "$target_gen_dir/tsc"
  tsconfig_base = "tsconfig_base.json"
  path_mappings = [
    "chrome://customize-chrome-side-panel.top-chrome/*|" + rebase_path(
            "$root_gen_dir/chrome/browser/resources/side_panel/customize_chrome/tsc/*",
            target_gen_dir),
    "chrome://webui-test/*|" +
        rebase_path("$root_gen_dir/chrome/test/data/webui/tsc/*",
                    target_gen_dir),
  ]
  in_files = [
    "shortcuts_test.ts",
    "test_support.ts",
    "app_test.ts",
    "appearance_test.ts",
    "categories_test.ts",
    "check_mark_wrapper_test.ts",
    "color_test.ts",
    "colors_test.ts",
    "themes_test.ts",
    "theme_snapshot_test.ts",
  ]
  definitions = [ "//tools/typescript/definitions/metrics_private.d.ts" ]
  deps = [
    "../..:build_ts",
    "//chrome/browser/resources/side_panel/customize_chrome:build_ts",
  ]
}

generate_grd("build_grdp") {
  grd_prefix = "webui_side_panel_customize_chrome"
  out_grd = "$target_gen_dir/resources.grdp"

  deps = [ ":build_ts" ]
  manifest_files =
      filter_include(get_target_outputs(":build_ts"), [ "*.manifest" ])
  resource_path_prefix = "side_panel_customize_chrome"
}
