# Copyright 2022 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//chrome/browser/resources/tools/build_webui.gni")

build_webui("build") {
  grd_prefix = "indexed_db"

  static_files = [
    "indexeddb_internals.html",
    "indexeddb_internals.css",
  ]

  non_web_component_files = [ "indexeddb_internals.ts" ]

  ts_deps = [
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]

  mojom_folder = "$root_gen_dir/mojom-webui"
  indexeddb_internal_folder = "content/browser/indexed_db"
  indexeddb_bucket_folder = "components/services/storage/privileged/mojom"
  buckets_folder = "components/services/storage/public/mojom/buckets"
  quota_folder = "third_party/blink/public/mojom/quota"
  storage_key_folder = "third_party/blink/public/mojom/storage_key"
  schemeful_site_folder = "services/network/public/mojom"

  mojo_files = [
    "$mojom_folder/$buckets_folder/bucket_id.mojom-webui.js",
    "$mojom_folder/$buckets_folder/bucket_locator.mojom-webui.js",
    "$mojom_folder/$indexeddb_bucket_folder/indexed_db_bucket_types.mojom-webui.js",
    "$mojom_folder/$indexeddb_internal_folder/indexed_db_internals.mojom-webui.js",
    "$mojom_folder/$quota_folder/quota_types.mojom-webui.js",
    "$mojom_folder/$schemeful_site_folder/schemeful_site.mojom-webui.js",
    "$mojom_folder/$storage_key_folder/ancestor_chain_bit.mojom-webui.js",
    "$mojom_folder/$storage_key_folder/storage_key.mojom-webui.js",
  ]

  mojo_files_deps = [
    "//$buckets_folder:buckets_js__generator",
    "//$indexeddb_bucket_folder:mojom_bucket_js__generator",
    "//$indexeddb_internal_folder:mojo_bindings_js__generator",
    "//$quota_folder:quota_js__generator",
    "//$schemeful_site_folder:mojom_schemeful_site_js__generator",
    "//$storage_key_folder:storage_key_js__generator",
  ]

  grit_output_dir = "$root_gen_dir/content"
}
