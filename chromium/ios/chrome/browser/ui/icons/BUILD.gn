# Copyright 2016 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/buildflag_header.gni")

declare_args() {
  # This defines targets to include branded icons.
  ios_branded_icons = ":branded"
}

_use_branded_symbols = get_label_info(":branded", "label_no_toolchain") !=
                       get_label_info(ios_branded_icons, "label_no_toolchain")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "IOS_USE_BRANDED_SYMBOLS=$_use_branded_symbols" ]
}

source_set("icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "chrome_icon.h",
    "chrome_icon.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/app/strings",
    "//third_party/material_design_icons:ic_arrow_back",
    "//third_party/material_design_icons:ic_chevron_right",
    "//third_party/material_design_icons:ic_close",
    "//third_party/material_design_icons:ic_info",
    "//third_party/material_design_icons:ic_search",
    "//ui/base",
  ]
  frameworks = [
    "CoreGraphics.framework",
    "UIKit.framework",
  ]
}

source_set("infobar_icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "infobar_icon.h",
    "infobar_icon.mm",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("action_icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "action_icon.h",
    "action_icon.mm",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("download_icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "download_icon.h",
    "download_icon.mm",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("item_icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "item_icon.h",
    "item_icon.mm",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("settings_icons") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "settings_icon.h",
    "settings_icon.mm",
  ]
  deps = [
    ":buildflags",
    ":symbols",
    "//ios/chrome/browser/ui/icons/resources:arrow_triangle_slash_circlepath",
    "//ios/chrome/browser/ui/icons/resources:checkerboard_shield",
    "//ios/chrome/browser/ui/icons/resources:checkermark_shield",
    "//ios/chrome/browser/ui/icons/resources:line_downtrend",
  ]
  frameworks = [ "UIKit.framework" ]
}

source_set("symbols") {
  configs += [ "//build/config/compiler:enable_arc" ]
  sources = [
    "chrome_symbol.h",
    "chrome_symbol.mm",
  ]
  deps = [
    "//base",
    "//ios/chrome/browser/ui:feature_flags",
    "//ios/chrome/browser/ui/icons/resources:arrow_clockwise",
    "//ios/chrome/browser/ui/icons/resources:camera",
    "//ios/chrome/browser/ui/icons/resources:camera_fill",
    "//ios/chrome/browser/ui/icons/resources:camera_lens",
    "//ios/chrome/browser/ui/icons/resources:incognito",
    "//ios/chrome/browser/ui/icons/resources:incognito_circle_fill",
    "//ios/chrome/browser/ui/icons/resources:incognito_circle_fill_ios14",
    "//ios/chrome/browser/ui/icons/resources:language",
    "//ios/chrome/browser/ui/icons/resources:laptopcomputer_and_phone",
    "//ios/chrome/browser/ui/icons/resources:password",
    "//ios/chrome/browser/ui/icons/resources:password_manager",
    "//ios/chrome/browser/ui/icons/resources:photo_badge_magnifyingglass",
    "//ios/chrome/browser/ui/icons/resources:photo_badge_plus",
    "//ios/chrome/browser/ui/icons/resources:plus_circle_fill",
    "//ios/chrome/browser/ui/icons/resources:popup_badge_minus",
    "//ios/chrome/browser/ui/icons/resources:square_bullet_square",
    "//ios/chrome/browser/ui/icons/resources:square_number",
    "//ios/chrome/browser/ui/icons/resources:translate",
    "//ui/base",
    ios_branded_icons,
  ]
  frameworks = [
    "CoreGraphics.framework",
    "UIKit.framework",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "chrome_icon_unittest.mm" ]
  deps = [
    ":icons",
    "//testing/gtest",
    "//third_party/ocmock",
    "//ui/base",
  ]
  configs += [ "//build/config/compiler:enable_arc" ]
}

# TODO(crbug.com/734054): remove this target when the downstream repository
# has been fixed to directly depends on the individual targets.
group("assets") {
  deps = [
    "//third_party/material_design_icons:ic_arrow_back",
    "//third_party/material_design_icons:ic_chevron_right",
    "//third_party/material_design_icons:ic_close",
    "//third_party/material_design_icons:ic_info",
    "//third_party/material_design_icons:ic_search",
  ]
}

group("branded") {
  # Empty in the non-branded repo.
}
