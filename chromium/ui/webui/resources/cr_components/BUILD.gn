# Copyright 2018 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

assert(!is_android && !is_ios)

preprocess_folder =
    "$root_gen_dir/ui/webui/resources/preprocessed/cr_components"

group("preprocess") {
  public_deps = [
    ":preprocess_generated_ts",
    ":preprocess_src_ts",
  ]
}

# TS files are passed to a separate target so that they are not listed in the
# |out_manifest|.
preprocess_if_expr("preprocess_src_ts") {
  in_folder = "."
  out_folder = preprocess_folder
  in_files = [
    "localized_link/localized_link.ts",
    "managed_dialog/managed_dialog.ts",
    "managed_footnote/managed_footnote.ts",
  ]
}

# TS files are passed to a separate target so that they are not listed in the
# |out_manifest|.
preprocess_if_expr("preprocess_generated_ts") {
  deps = [ ":html_wrapper_files" ]
  in_folder = target_gen_dir
  out_folder = preprocess_folder
  in_files = [
    "localized_link/localized_link.html.ts",
    "managed_dialog/managed_dialog.html.ts",
    "managed_footnote/managed_footnote.html.ts",
  ]
}

html_to_wrapper("html_wrapper_files") {
  in_files = [
    "localized_link/localized_link.html",
    "managed_dialog/managed_dialog.html",
    "managed_footnote/managed_footnote.html",
  ]
}
