# Static feature overrides, don't change
# Features we either always want enable or don't want at all,
# depending on platform settings

import("//vivaldi/gn/config/features.gni")
import("//vivaldi/gn/config/product.gni")
import("//vivaldi/gn/config/hermetic.gni")
import("//vivaldi/gn/config/reclient.gni")

import("//build/toolchain/cc_wrapper.gni")
import("//build/toolchain/goma.gni")
import("//build/toolchain/rbe.gni")

declare_overrides() {
  if(!is_ios) {
    branding_path_component = "vivaldi"
    branding_path_product = "vivaldi"
  } else {
    # We use google_chrome strings
    # IOS do not have other ways to select the string
    # However, the variable is only used to select the variant strings
    branding_path_component = "google_chrome"
    branding_path_product = "google_chrome"
  }
  branding_file_path = "//vivaldi/app/resources/theme/vivaldi/BRANDING"

  if (is_linux || is_mac || is_win) {
    # ffmpeg_branding == Chromium disables way too much code including various
    # utilities unrelated to codecs that we need for system proprietary media
    # code. Instead of re-enabling the missing pieces we will black list
    # relevant codecs explicitly in ffmpeg.
    ffmpeg_branding = "Chrome"
  }
  proprietary_codecs = true
  dcheck_always_on = is_debug # || is_component_build

  if(!is_ios) {
    enable_widevine = true

    enable_media_remoting = true
  }

  if (is_linux) {
    if (!is_component_build) {
      enable_linux_installer = is_linux
      # enable_snap_package = true
    }
    if (is_official_build) {
      symbol_level = 1
    }
    enable_js_type_check = false
  }

  if (is_android) {
    if (is_official_build) {
      symbol_level = 1
    }
    enable_gvr_services = false
    enable_vr = false
    ffmpeg_branding = "Chrome"
    android_channel = "stable"
    chrome_public_manifest_package = android_vivaldi_manifest_package
    if (!is_debug) {
      if (is_official_build) {
        enable_resource_allowlist_generation = false
      }
      strip_debug_info = true
    }
  }

  if(!is_ios) {
    enable_hangout_services_extension = true
  }

  enable_precompiled_headers = !is_official_build && cc_wrapper == "" && !use_goma && !use_remoteexec

  if (is_win) {
    skip_archive_compression = is_component_build
    use_large_pdbs = !is_component_build && !is_debug
  }

  if (is_mac) {
    use_system_xcode = true
    #if (getenv("CHROME_HEADLESS") == "1") {
    #  mac_use_sdk = "12.3"
    #}
  }

  # Needed until we discover what is causing the unused argument during clcache
  if (is_win && cc_wrapper != "") {
    use_ghash = false
  }

  if ((target_cpu == "arm" || target_cpu == "arm64") && !is_ios) {
    enable_openscreen_protocol = false
  }

  if (is_win && defined(use_hermetic_toolchain) && use_hermetic_toolchain) {
    toolchain_json_spec_file = "//vivaldi/build/toolchain.json"
  }

  if(!is_ios) {
    enable_cdm_host_verification = (is_mac || is_win) && is_official_build && !vivaldi_disable_widevine_verification
    enable_cdm_storage_id = (is_mac || is_win) && is_official_build && !vivaldi_disable_widevine_verification
  }
  if(is_ios) {
    ios_app_bundle_id_prefix = "com.vivaldi"
    ios_enable_code_signing = vivaldi_signing_id != ""
    ios_code_signing_identity = vivaldi_signing_id
    ios_code_signing_identity_description = ""
    is_component_build = false

    ios_application_icons_target = "//vivaldi/ios/app/resources:vivaldi_icons"
    ios_launchscreen_assets_target = "//vivaldi/ios/app/resources:launchscreen_assets"

    ios_chrome_links_with_lottie_framework=false
  }
}
